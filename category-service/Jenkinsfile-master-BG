def deploy_to_new_stack(env_name, isFastDeploy, buildNumber = env.BUILD_NUMBER) {
  stage("Deploy to ${env_name} (${buildNumber})") {
     build(job: "deploy-to-${env_name}", parameters: [
        [$class: 'StringParameterValue', name: 'image_tag', value: "${brand_prefix}-${buildNumber}"],
        [$class: 'StringParameterValue', name: 'environment', value: "${env_name}"],
        [$class: 'StringParameterValue', name: 'service_name', value: 'category-service'],
		[$class: 'StringParameterValue', name: 'service_name_config', value: 'category-service'],
        [$class: 'StringParameterValue', name: 'image_repo', value: 'category-service'],
        [$class: 'BooleanParameterValue', name: 'fast_drain', value: isFastDeploy]
     ])
  }
}

final red = '#FF0000'
final searchNavChannel = "sixthday_search_navigation"
final slackMsgPipelineLink = "<${env.JOB_URL}|category-service master pipeline>"
final slackMsgConsoleLink = "<${env.BUILD_URL}console|Console Output>"

node('build && ctp') {
    def ecr_repo_hostname = env.ECR_REPO_HOSTNAME
    def ecr_repo_name = "category-service"
    def aws_repo_url = "${ecr_repo_hostname}/${ecr_repo_name}"
    def prod_artifactory_url = 'http://jfrog.mynmg.com'

    def env_dev_name = "bgdev"

    def env_dev_int_name = "bgdevint"

    def docker_image_name = "${aws_repo_url}:${brand_prefix}-${BUILD_NUMBER}"
    def docker_deploy_image_name = "${docker_image_name}-deploy"
    def branch = env.BRANCH ?: "*/master"

    step([$class: 'StashNotifier'])

    try {
        timestamps {
            stage('Checkout') {
                checkout scm
                sh "./clean_old_docker_images clean_docker_output.txt"
            }

            stage('Clean and build') {
                withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'PROD_ARTIFACTORY_CREDENTIALS',
                                  usernameVariable: 'ARTIFACTORY_USER', passwordVariable: 'ARTIFACTORY_PASSWORD']]) {
                    sh "./gradlew clean build -x test -PartifactoryUrl=${prod_artifactory_url} -PartifactoryUser=${ARTIFACTORY_USER} -PartifactoryPassword=${ARTIFACTORY_PASSWORD} -PbuildInfo=true -Pbuild.job.name=${JOB_NAME} -Pbuild.job.number=${BUILD_NUMBER}"
                }
            }

            parallel([
                unitTestAndCodeQuality: {
                    stage('Unit test and Code-quality check (SonarQube)') {
                        withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'SONAR_HOST_AND_TOKEN',
                                          usernameVariable: 'SONAR_HOST_URL', passwordVariable: 'SONAR_AUTH_TOKEN']]) {
                            sh "./gradlew sonarqube -Dsonar.host.url=$SONAR_HOST_URL -Dsonar.login=$SONAR_AUTH_TOKEN"
                        }
                    }
                },
                serviceTests: {
                    stage('Run Service Tests') {
                        sh "./gradlew serviceTest"
                        archiveArtifacts 'build/libs/category-service-0.0.1-SNAPSHOT.jar'
                    }
                },  providerContractTest: {
                    stage('Run and Verify provider contract Tests') {
                        withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'PACTBROKER_CREDENTIALS',
                                        usernameVariable: 'PACTBROKER_USER', passwordVariable: 'PACTBROKER_PASSWORD']]) {
                            sh './gradlew providerContractTest -PpactBrokerUserName=${PACTBROKER_USER} -PpactBrokerPassword=${PACTBROKER_PASSWORD}'
                        }
                    }
                }, securityAudit: {
                    stage('OWASP Dependency Check') {
                        withCredentials([[$class          : 'UsernamePasswordMultiBinding', credentialsId: 'ARTIFACTORY_API_CREDENTIALS_READ_ONLY',
                                          usernameVariable: 'ARTIFACTORY_API_USER', passwordVariable: 'ARTIFACTORY_API_KEY']]) {
                            sh "./gradlew dependencyCheckAnalyze -PartifactoryUrl=${prod_artifactory_url} -PplatopsRepoKey=${ARTIFACTORY_API_KEY} --refresh-dependencies"
                        }
                    }
                }
            ])

            // Separated this stage because if we run it right after Run SonarQube stage, it's picking the old report.
            stage('Validate SonarQube QualityGate') {
                withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'SONAR_HOST_AND_TOKEN',
                                          usernameVariable: 'SONAR_HOST_URL', passwordVariable: 'SONAR_AUTH_TOKEN']]) {
                    sh "./read_sonar_qube_status.sh $SONAR_HOST_URL category-service"
                }
            }

            stage('Build and Publish to ECR') {
                sh "docker build -t ${docker_image_name} ."

                sh "docker build -t ${docker_deploy_image_name} -f ./Dockerfile-deploy ."

                tag_and_push_docker_image_to_ECR(aws_repo_url, docker_image_name, "${BUILD_NUMBER}")
                tag_and_push_docker_image_to_ECR(aws_repo_url, docker_deploy_image_name, "${BUILD_NUMBER}-deploy")
            }

            lock("Stages-CreateAndDeployService_${env_dev_name}") {
                try {
                    deploy_to_new_stack(env_dev_name, false)

                    stage('Run Integration Tests') {
                       	sh './gradlew -Dspring.profiles.active=bgdev -Dspring.cloud.vault.token=$(aws s3 cp s3://vault-bgdev-east/dev/access_token.txt -) integrationTest'
        			}

                } catch (error) {
                    deploy_to_new_stack(env_dev_name, false, "bronze")
                    throw (error)
                }
            }

            stage('Tag bronze') {
                tag_and_push_docker_image_to_ECR(aws_repo_url, docker_image_name, "bronze")
                tag_and_push_docker_image_to_ECR(aws_repo_url, docker_deploy_image_name, "bronze-deploy")
            }

            stage('Test Integration with Dev Int') {
                sh "./gradlew dockerTestImage --imageName ${docker_image_name} --env bgdevint --buildNumber ${BUILD_NUMBER} --envVersionName bgdevint --vaultAccessTokenPath=s3://vault-bgdev-east/dev/access_token.txt"
            }

            lock("Stages-CreateAndDeployService_${env_dev_int_name}") {
                deploy_to_new_stack(env_dev_int_name, false)

                stage('Run Performance Tests') {
                    sh 'cd performance && SCALA_ENV=bgdevint sbt gatling:test && cd -'
                    gatlingArchive()
                }
            }

            stage('Tag silver') {
                tag_and_push_docker_image_to_ECR(aws_repo_url, docker_image_name, "silver")
                tag_and_push_docker_image_to_ECR(aws_repo_url, docker_deploy_image_name, "silver-deploy")
            }
            update_jira_fix_version()
            currentBuild.result = 'SUCCESS'
        }
    } catch (error) {
        currentBuild.result = 'FAILED'
        slackSend channel: "${searchNavChannel}", color: "${red}", message: "Build ${env.BUILD_NUMBER} FAILED for ${slackMsgPipelineLink}.  (${slackMsgConsoleLink})"        
    } finally {
        publish_test_reports()
        step([$class: 'StashNotifier'])
    }
}

def tag_and_push_docker_image_to_ECR(aws_repo_url, docker_image_name, tag_name) {
    def brand_tag_name = "${brand_prefix}-${tag_name}"
	def docker_tag_name = "${aws_repo_url}:${brand_tag_name}"
    sh "docker tag ${docker_image_name} ${docker_tag_name}"

    push_return_code = docker_push_current_image(docker_tag_name)
    if (push_return_code != 0) {
        login_cmd = sh(
                script: 'aws ecr get-login --no-include-email',
                returnStdout: true
        ).trim()

        sh "${login_cmd}"
        push_return_code = docker_push_current_image(docker_tag_name)
        if (push_return_code != 0) {
            error("Failed to push docker image: ${docker_tag_name}")
        }
    }
}

def docker_push_current_image(docker_image_name) {
    sh(
            script: "docker push ${docker_image_name}",
            returnStatus: true
    )
}

def login_to_ecr() {
    login_cmd = sh(
            script: 'aws ecr get-login --no-include-email',
            returnStdout: true
    ).trim()

    sh "${login_cmd}"
}

def update_jira_fix_version() { 
	sh "git log --format=oneline -n 1 | awk 'match(\$0, /[A-Z]+-[0-9]+/) { print substr(\$0, RSTART, RLENGTH) }' > COMMIT_MSG"
	def git_commit = readFile('COMMIT_MSG').trim()
	def fixVersion = getFixVersion()
	withCredentials([[$class: 'UsernamePasswordMultiBinding', 
	credentialsId: 'JIRA_CREDENTIALS', 
	usernameVariable: 'NM_JIRA_USER', 
	passwordVariable: 'NM_JIRA_PWD']]) { 
		sh "curl -u $NM_JIRA_USER:$NM_JIRA_PWD -H \"Content-Type: application/json\" -X PUT https://jira.mynmg.com/rest/api/2/issue/${git_commit} --data '{\"update\": {\"fixVersions\": [{\"set\": [{\"name\": \"${fixVersion}\"}]}]}}'" 
	}    
}
@NonCPS
def getFixVersion() {
    def dateFormat = new java.text.SimpleDateFormat("yy.M.W")
    def date = new Date().with {
        d -> d + (7 + Calendar.MONDAY - d[Calendar.DAY_OF_WEEK])%7
    }
    dateFormat.format(date)
}
def publish_test_reports() {
    publishHTML([
            allowMissing         : false,
            alwaysLinkToLastBuild: true,
            keepAll              : true,
            reportDir            : 'build/reports/tests/test',
            reportFiles          : 'index.html',
            reportName           : 'Unit test report'
    ])
    publishHTML([
            allowMissing         : false,
            alwaysLinkToLastBuild: true,
            keepAll              : false,
            reportDir            : 'build/reports/tests/serviceTest',
            reportFiles          : 'index.html',
            reportName           : 'Service test report'
    ])
    publishHTML([
            allowMissing         : false,
            alwaysLinkToLastBuild: true,
            keepAll              : false,
            reportDir            : 'build/reports/tests/providerContractTest',
            reportFiles          : 'index.html',
            reportName           : 'Provider contract test report'
    ])
    publishHTML([
            allowMissing         : false,
            alwaysLinkToLastBuild: true,
            keepAll              : false,
            reportDir            : 'build/reports/tests/integrationTest',
            reportFiles          : 'index.html',
            reportName           : 'Integration test report'
    ])
    publishHTML([
            allowMissing         : false,
            alwaysLinkToLastBuild: true,
            keepAll              : false,
            reportDir            : 'build/reports',
            reportFiles          : 'dependency-check-report.html',
            reportName           : 'Dependency check report'
    ])
    publishHTML([
            allowMissing         : true,
            alwaysLinkToLastBuild: true,
            keepAll              : true,
            reportDir            : 'build/reports/clean_old_docker_iamges',
            reportFiles          : "clean_docker_output.txt",
            reportName           : 'Clean Old Docker Images Report'
    ])
}
