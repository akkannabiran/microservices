buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
        springCloudVersion = 'Dalston.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.6.2"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'


repositories {
    jcenter()
}

allprojects {
    ext.baseVersion = "0.0." + (project.hasProperty('version') ? project.version : "1")
    ext.snapshotVersion = !project.hasProperty('version')
    group = "com.nmo"
    version = "$baseVersion" + (snapshotVersion ? "-SNAPSHOT" : "-RELEASE")
}

bootRepackage {
    enabled = false
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.slf4j:slf4j-api:1.7.25')
    compile('org.apache.logging.log4j:log4j-slf4j-impl:2.8.1')
    compile('org.apache.logging.log4j:log4j-core:2.8.1')
    compile('org.apache.logging.log4j:log4j-api:2.8.1')
    compile('org.apache.logging.log4j:log4j-jcl:2.8.1')
    compile('org.projectlombok:lombok:1.16.12')
    compile('junit:junit:4.12')
    compile('org.springframework.cloud:spring-cloud-starter-hystrix')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('junit:junit:4.12')
}

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

artifactory {
    contextUrl = project.hasProperty('artifactoryUrl') ? project.artifactoryUrl : 'http://jfrog.mynmg.com'
    publish {
        repoKey = 'libs-nmo-dt-local'
        username = project.hasProperty('artifactoryUser') ? project.artifactoryUser : ''
        password = project.hasProperty('artifactoryPassword') ? project.artifactoryPassword : ''

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

